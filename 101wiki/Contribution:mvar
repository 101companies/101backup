== Headline ==

[[Concurrent programming]] in [[Language:Haskell]] with [[MVar]]s

== Motivation ==

The implementation demonstrates [[concurrent programming]] in [[Language:Haskell]] with Haskell's [[MVar]]s, i.e., thread synchronization variables. To this end, [[task parallelism]] is leveraged in a manner that computations for the operations for totaling and cutting salaries are organized in multiple threads. In fact, a new thread is created for each new sub-department as it is encountered along the computation. The result of from each thread is stored in an [[MVar]] and then collected and aggregated with other results by the "upper" thread. Clearly, if efficiency rather than illustration was the objective here, then a more resource-aware strategy is needed. 

== Illustration ==

=== Concurrent cutting ===

We create new threads using <syntaxhighlight lang="haskell" enclose="none">forkIO :: IO () -> IO ThreadId</syntaxhighlight> provided by Haskell's concurrency library <syntaxhighlight lang="haskell" enclose="none">Control.Concurrent</syntaxhighlight>. This function executes the given IO action in a new thread and returns a <syntaxhighlight lang="haskell" enclose="none">ThreadId</syntaxhighlight> value. On the top company level we do so for every department:

<syntaxhighlight lang="haskell">
cutCompany :: Company -> IO Company
cutCompany (Company n depts) = do
    mvars <- forM depts $ \d -> do
        mvar' <- newEmptyMVar
        forkIO $ cutDept mvar' d
        return mvar'
    cutDepts <- takeAllMVars mvars
    return $ Company n cutDepts
</syntaxhighlight>

We iterate over the departments by making use of <syntaxhighlight lang="haskell" enclose="none">forM</syntaxhighlight> in line 3. For each department we create a new empty <syntaxhighlight lang="haskell" enclose="none">MVar</syntaxhighlight> value, which we then pass to the cut function, which we start in a new thread. We collect all <syntaxhighlight lang="haskell" enclose="none">MVar</syntaxhighlight> values in <syntaxhighlight lang="haskell" enclose="none">mvars</syntaxhighlight>. In line 7 we wait for the results of the computations. The new company is returned in line 8. Similar to this we cut departments:

<syntaxhighlight lang="haskell">
cutDept :: MVar Department -> Department -> IO ()
cutDept mvar (Department n m dus eus) = do
    mvars <- forM dus $ \d -> do
        mvar' <- newEmptyMVar
        forkIO $ cutDept mvar' d
        return mvar'
    cutDus <- takeAllMVars mvars
    putMVar mvar $ Department n (cutEmployee m) 
                                (cutDus) 
                                (map cutEmployee eus)    
</syntaxhighlight>

The difference to <syntaxhighlight lang="haskell" enclose="none">cutCompany</syntaxhighlight> is that <syntaxhighlight lang="haskell" enclose="none">cutDept</syntaxhighlight> puts the new department in a given <syntaxhighlight lang="haskell" enclose="none">MVar</syntaxhighlight> value.

The cutting of direct department employees is not performed in a new thread:

<syntaxhighlight lang="haskell">
cutEmployee :: Employee -> Employee
cutEmployee (Employee name address salary) = Employee name address $ salary / 2
</syntaxhighlight>

=== Collecting results ===

Both functions <syntaxhighlight lang="haskell" enclose="none">cutCompany</syntaxhighlight> and <syntaxhighlight lang="haskell" enclose="none">cutDept</syntaxhighlight> need to wait for the child-threads to terminate. To do so we provide a function <syntaxhighlight lang="haskell" enclose="none">takeAllMVars</syntaxhighlight>:

<syntaxhighlight lang="haskell">
takeAllMVars ::  [MVar a] -> IO [a]
takeAllMVars = mapM takeMVar
</syntaxhighlight> 

This function takes all <syntaxhighlight lang="haskell" enclose="none">MVar</syntaxhighlight> values one by one blocking on every empty MVar.

== Architecture ==

[this!!Total.hs] and [this!!Cut.hs] provide functionality for totaling and cutting salaries in a concurrent way. 
[this!!Utils.hs] contains a function to collect content of a list of <syntaxhighlight lang="haskell" enclose="none">MVar</syntaxhighlight> values.
The algebraic datatype for companies can be found in [this!!Company.hs]. 
[this!!Main.hs] collects test scenarios for totaling and cutting a sample company hosted by [this!!SampleCompany.hs].

== Usage ==

* [this!!Main.hs] has to be loaded into GHCi. 
* The <syntaxhighlight lang="haskell" enclose="none">main</syntaxhighlight> function has to be applied.
* The output should be equal to the content of the file [this!!baseline].
One can also use the [this!!Makefile] with a target ''test'' for [[test automation]].

== Issues ==

* The current implementation does not address the problem of a possibly unbalanced department tree.
* The collection function for MVars blocks on every empty element. We may need a more sophisticated collection function.

== Metadata ==

* [[uses::Language:Haskell]]
* [[uses::Technology:GHCi]]      
* [[implements::Feature:Hierarchical_company]]
* [[implements::Feature:Total]]
* [[implements::Feature:Cut]]
* [[implements::Feature:Task parallelism]]
* [[developedBy::Contributor:Thomas Schmorleiz]]
* [[instanceOf::Theme:Haskell potpourri]]
* [[instanceOf::Namespace:Contribution]]